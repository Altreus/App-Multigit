#!/usr/bin/env perl

use strict;
use warnings;
use 5.014;

use Getopt::Long qw(:config gnu_getopt pass_through);
use App::Multigit;
use Future;

my $workdir;
GetOptions(
    'help|h' => sub {
        say usage();
        exit 0;
    },
    'workdir=s' => \$workdir,
);

chdir $workdir;

my @futures = 
    map { $_->then(report()) }
    App::Multigit::each([qw(git branch), @ARGV])
;

say for grep $_, sort Future->needs_all(@futures)->get;

sub report {
    sub {
        my ($repo, $config, $pid, $exitcode, $stdout, $stderr) = @_;
        my $dir = $config->{dir};
        Future->done( "$dir:\n$stdout");
    }
}

sub usage {
<<EOU;
Usage:
    mg branch [argv]

    Runs `git branch argv` on each repository. See `git help branch`.

    With no arguments, therefore, this just lists the branches in each
    repository.
EOU
}
